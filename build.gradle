plugins {
   id "com.gradle.plugin-publish" version "1.0.0"
   id "com.github.breadmoirai.github-release" version "2.4.1"
   id 'groovy'
   id 'java-gradle-plugin'
   id 'org.unbroken-dome.test-sets' version '4.0.0'
   id "com.github.ben-manes.versions" version "0.42.0"
   id 'com.adarshr.test-logger' version '3.2.0'
   id 'build-dashboard'
}
ext.pluginName = rootProject.name

task cleanJunit(type: Delete) {
   delete getTestResultsDir()
}

task cleanLibs(type: Delete) {
   delete jar.destinationDirectory.files()
}

java {
   toolchain {
      languageVersion = JavaLanguageVersion.of(11)
   }
}

githubRelease {
   token findProperty('githubToken').toString()
   owner 'RedPillAnalytics'
   repo rootProject.name
   overwrite true
   releaseAssets jar.destinationDirectory.files()
}

dependencies {
   implementation 'org.apache.groovy:groovy:4.0.5'
   implementation 'org.eclipse.jgit:org.eclipse.jgit:6.3.0.202209071007-r'
   testImplementation 'org.spockframework:spock-core:2.2-groovy-4.0'
}

tasks.withType(GroovyCompile).configureEach {
    options.forkOptions.jvmArgs << '-Dspock.iKnowWhatImDoing.disableGroovyVersionCheck=true'    //it works! But be aware that "options" not "groovyOptions"!
}

// Default artifact naming.
group = 'com.redpillanalytics'

repositories {
   mavenCentral()
   maven {
      url "https://plugins.gradle.org/"
   }
}

pluginBundle {

   description = "Assists Plugin developers with assigning Gradle project properties to properties in the Plugin Extension."
   website = "https://github.com/RedPillAnalytics/${rootProject.name}/"
   vcsUrl = "https://github.com/RedPillAnalytics/${rootProject.name}/"
   tags = ['project-properties','plugin-development']
}

gradlePlugin {
   plugins {
      gradleProperties {
         id = "com.redpillanalytics.${pluginName}"
         displayName = 'Gradle Properties'
         implementationClass = 'com.redpillanalytics.gradle.properties.GradlePlugin'
      }
   }
}

// Options for all tests
task runAllTests {
   description 'Run all defined tests.'
   group 'verification'
}

tasks.withType(Test) {
   tasks.runAllTests.dependsOn it
   failFast true
   systemProperty 'projectDir', temporaryDir
   useJUnitPlatform()
}

tasks.githubRelease.mustRunAfter tasks.publishPlugins
tasks.publish.dependsOn tasks.publishPlugins, tasks.githubRelease, tasks.groovydoc

