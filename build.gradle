import org.gradle.jvm.toolchain.JavaLanguageVersion

plugins {
   id "com.gradle.plugin-publish" version "0.13.0"
   id "com.github.breadmoirai.github-release" version "2.2.12"
   id 'groovy'
   id 'java-gradle-plugin'
   id 'org.unbroken-dome.test-sets' version '3.0.1'
   id "com.github.ben-manes.versions" version "0.38.0"
   id 'com.adarshr.test-logger' version '2.1.1'
   id 'build-dashboard'
}
ext.pluginName = rootProject.name

task cleanJunit(type: Delete) {
   delete getTestResultsDir()
}

task cleanLibs(type: Delete) {
   delete getLibsDir()
}

java {
   toolchain {
      languageVersion = JavaLanguageVersion.of(8)
   }
}

githubRelease {
   token findProperty('githubToken')
   owner 'RedPillAnalytics'
   repo rootProject.name
   overwrite true
   releaseAssets jar.destinationDirectory.files()
}

dependencies {
   implementation localGroovy()
   implementation 'org.slf4j:slf4j-simple:1.7.30'
   implementation 'org.eclipse.jgit:org.eclipse.jgit:5.11.0.202103091610-r'
   testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

// Default artifact naming.
group = 'com.redpillanalytics'

repositories {
   jcenter()
   maven {
      url "https://plugins.gradle.org/"
   }
}

gradlePlugin {
   plugins {
      pluginTemplate {
         id = "com.redpillanalytics.${pluginName}"
         implementationClass = 'com.redpillanalytics.gradle.properties.GradlePlugin'
      }
   }
}

pluginBundle {

   description = "Assists Plugin developers with assigning Gradle project properties to properties in the Plugin Extension."
   website = "https://github.com/RedPillAnalytics/${rootProject.name}/"
   vcsUrl = "https://github.com/RedPillAnalytics/${rootProject.name}/"

   plugins {
      pluginTemplate {
         id = "com.redpillanalytics.${pluginName}"
         displayName = pluginName
         tags = ['project-properties','plugin-development']
         version = project.version
      }
   }
}

// Options for all tests
task runAllTests {
   description 'Run all defined tests.'
   group 'verification'
}

tasks.withType(Test) {
   tasks.runAllTests.dependsOn it
   failFast true
   systemProperty 'projectDir', temporaryDir
}

// create a publish task
task publish {
   description 'Custom publish task.'
   group 'publishing'
   dependsOn tasks.publishPlugins, tasks.githubRelease
}

tasks.githubRelease.mustRunAfter tasks.publishPlugins

